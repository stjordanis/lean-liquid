import rescale.basic
import locally_constant.Vhat

import for_mathlib.preadditive_category
import for_mathlib.additive_functor

import facts.nnreal

noncomputable theory
open_locale big_operators classical nnreal

local attribute [-instance] add_comm_monoid.nat_semimodule add_comm_group.int_module

namespace rescale

variables {N : ‚Ñù‚â•0} {V : Type*}

instance [has_norm V] : has_norm (rescale N V) :=
{ norm := Œª v, ‚à•of.symm v‚à•/N }

lemma norm_def [has_norm V] (v : rescale N V) : ‚à•v‚à• = ‚à•of.symm v‚à•/N := rfl

-- remove the `fact` once we have `seminormed_group`
instance [hN : fact (0 < N)] [normed_group V] : normed_group (rescale N V) :=
normed_group.of_core (rescale N V)
{ norm_eq_zero_iff := Œª v,
  begin
    have aux : (N:‚Ñù) ‚â† 0 := ne_of_gt hN.out,
    simp only [norm_def, div_eq_zero_iff, aux, or_false],
    exact norm_eq_zero -- defeq abuse
  end,
  triangle := Œª v w,
  begin
    simp only [norm_def, ‚Üê add_div],
    exact div_le_div_of_le hN.out.le (norm_add_le _ _), -- defeq abuse
  end,
  norm_neg := Œª v, by { simp only [norm_def], congr' 1, exact norm_neg _ /- defeq abuse -/ } }

lemma nnnorm_def [hN : fact (0 < N)] [normed_group V] (v : rescale N V) :
  nnnorm v = nnnorm (of.symm v) / N := rfl

end rescale

namespace NormedGroup

variables (r r‚ÇÅ r‚ÇÇ : ‚Ñù‚â•0) [fact (0 < r)] [fact (0 < r‚ÇÅ)] [fact (0 < r‚ÇÇ)]

@[simps]
def rescale (r : ‚Ñù‚â•0) [hr : fact (0 < r)] : NormedGroup ‚•§ NormedGroup :=
{ obj := Œª V, of $ rescale r V,
  map := Œª V‚ÇÅ V‚ÇÇ f,
  { to_fun := Œª v, @rescale.of r V‚ÇÇ $ f ((@rescale.of r V‚ÇÅ).symm v),
    map_add' := f.map_add, -- defeq abuse
    bound' :=
    begin
      obtain ‚ü®C, C_pos, hC‚ü© := f.bound,
      use C,
      dsimp,
      intro v,
      rw [rescale.norm_def, rescale.norm_def, ‚Üê mul_div_assoc, div_le_div_right],
      swap, { exact hr.out },
      exact hC _,
    end },
  map_id' := Œª V, rfl, -- defeq abuse
  map_comp' := Œª V‚ÇÅ V‚ÇÇ V‚ÇÉ f g, rfl /- defeq abuse -/ }

instance rescale.additive : (rescale r).additive :=
{ map_zero' := Œª V W, rfl, -- defeq abuse
  map_add' := Œª V W f g, rfl /- defeq abuse -/ }

def to_rescale : ùü≠ _ ‚ü∂ rescale r :=
{ app := Œª V,
  add_monoid_hom.mk_normed_group_hom' (add_monoid_hom.mk' (@rescale.of r V) $ Œª _ _, rfl) r‚Åª¬π
  begin
    intro v,
    dsimp,
    rw [rescale.nnnorm_def, div_eq_inv_mul],
    refl
  end,
  naturality' := Œª V W f, rfl /- defeq abuse -/ }

lemma to_rescale_bound_by (V : NormedGroup) : ((to_rescale r).app V).bound_by r‚Åª¬π :=
normed_group_hom.mk_normed_group_hom'_bound_by _ _ _

def scale : rescale r‚ÇÅ ‚ü∂ rescale r‚ÇÇ :=
{ app := Œª V,
  add_monoid_hom.mk_normed_group_hom'
    (add_monoid_hom.mk' (Œª v, (@rescale.of r‚ÇÇ V) $ (@rescale.of r‚ÇÅ V).symm v) $
      Œª _ _, rfl) (r‚ÇÅ / r‚ÇÇ)
  begin
    dsimp,
    intro v,
    simp only [rescale.nnnorm_def, add_monoid_hom.coe_mk', div_eq_inv_mul, equiv.symm_apply_apply],
    rw [mul_assoc, mul_inv_cancel_left'],
    have : fact (0 < r‚ÇÅ), assumption, exact this.out.ne'
  end,
  naturality' := Œª V W f, rfl /- defeq abuse -/ }

lemma scale_bound_by (V : NormedGroup) : ((scale r‚ÇÅ r‚ÇÇ).app V).bound_by (r‚ÇÅ / r‚ÇÇ) :=
normed_group_hom.mk_normed_group_hom'_bound_by _ _ _

end NormedGroup
